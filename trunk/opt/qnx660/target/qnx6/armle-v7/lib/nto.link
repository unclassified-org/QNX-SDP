/*
 * nto.link
 *	Loader command script for Neutrino
 *
 */
ENTRY(_start)
PHDRS {
	text	PT_LOAD;
	data	PT_LOAD; 
	note	PT_NOTE;
}

SECTIONS {
	.text 0x00108000 : {
		btext = .;
		_btext = .;
		*(.text.unlikely .text.*_unlikely)
		*(.text.exit .text.exit.*)
		*(.text.startup .text.startup.*)
		*(.text.hot .text.hot.*)
		*(.text .text.*)
		*(.text_kernel)
		*(.glue_7t) *(.glue_7)
		PROVIDE_HIDDEN (__rel_iplt_start = .);
		*(.rel.iplt)
		PROVIDE_HIDDEN (__rel_iplt_end = .);
		etext = .;
		_etext = .;
	} :text 
	.rodata ALIGN(0x1000) : {
		_fdata = .;
		*(.rodata_mod_start)
		module_list_start = .;	
		*(.rodata_mod)
		module_list_end = .;	
		*(.rodata_mod_end)
		*(.rodata*)
	} :data 
	.ARM.extab   : { 
		*(.ARM.extab* .gnu.linkonce.armextab.*)
	}
	 __exidx_start = .;
	.ARM.exidx   : {
		 *(.ARM.exidx* .gnu.linkonce.armexidx.*)
	}
	__exidx_end = .;
	.data . : {
		_data = .;
		*(.data)
		*(.eh_frame*)
	} :data
	.ctors . : {
		*(.ctors)
	}
	.got . : {
		_got = .;
		*(.got)
		*(.got2)
	} :data
	.sdata . : {
		_sdata = .;
		*(.sdata)
		edata = .;
		_edata = .;
	} :data
	.sbss . : {
		__bss_start = .;
		_fbss = .;
		*(.sbss) *(.scommon)
	} :data
	.bss . : {
		*(.bss)
		*(COMMON)
		_end = .;
		end = .;
	}
	.debug 0x0 : { *(.debug) }:NONE
	.debug_srcinfo 0x0 : { *(.debug_srcinfo) }:NONE
	.debug_aranges 0x0 : { *(.debug_aranges) }:NONE
	.debug_pubnames 0x0 : { *(.debug_pubnames) }:NONE
	.debug_sfnames 0x0 : { *(.debug_sfnames) }:NONE
	.line 0x0 : { *(.line) }:NONE
}
